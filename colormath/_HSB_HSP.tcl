# Developer:  ğŸ†ğŸ…¾ğŸ…±ğŸ…´ğŸ†ğŸ†ƒğŸ…¾  ğŸ…²ğŸ…°ğŸ…»ğŸ…°ğŸ…±ğŸ†ğŸ…´ğŸ†‚ğŸ…´
# City:       ğŸ…¿ğŸ…°ğŸ…»ğŸ…´ğŸ†ğŸ…¼ğŸ…¾, ğŸ†‚ğŸ…¸ğŸ…²ğŸ…¸ğŸ…»ğŸ†ˆ
# Country:    ğŸ…¸ğŸ†ƒğŸ…°ğŸ…»ğŸ†ˆ

# ::_HSB_HSP
#
# Transform HSB channels into HSP channels.
#
# Where:
#
# channels      Should be a list containing the HSB channels to convert, with:
#                   the hue channel in the range [0,360[,
#                   the saturation channel in the range [0,100.0],
#                   the brightness channel in the range [0,100.0].
#
# Note:  Some pre-computation have been made in order to increase the performance:
#           1.0/6.0   = 0.16666666666666666
#           1.0/100.0 = 0.01
#
# Note:  For info about unadapted values visit 'http://www.brucelindbloom.com'.
#
# Note:  For info about the HSP color system visit 'https://www.alienryderflex.com/hsp.html'.
#
# Returns a list containing the resulting HSP channels, with:
#   the hue channels in the range [0,360[,
#   the saturation channels in the range [0,100.0],
#   the perceived_brightness channels in the range [0,100.0].
proc ::_HSB_HSP { channels } {
    foreach { hue saturation brightness } $channels {
        set s [expr { $saturation*0.01 }]; # [0,1.0]
        set b [expr { $brightness*0.01 }]; # [0,1.0]

        set h [expr { int($hue*0.016666666666666666)%6 }]
        set f [expr { ($hue*0.016666666666666666)-$h }]
        set p [expr { $b*(1.0-$s) }]

        switch -- $h {
            0   {
                set k [expr { 1.0-((1.0-$f)*$s) }]
                set r $b
                set g [expr { $b*$k }]
                set b $p
            }
            1   {
                set k [expr { 1.0-($f*$s) }]
                set r [expr { $b*$k }]
                set g $b
                set b $p
            }
            2   {
                set k [expr { 1.0-((1.0-$f)*$s) }]
                set r $p
                set g $b
                set b [expr { $b*$k }]
            }
            3   {
                set k [expr { 1.0-($f*$s) }]
                set r $p
                set g [expr { $b*$k }]
                set b $b
            }
            4   {
                set k [expr { 1.0-((1.0-$f)*$s) }]
                set r [expr { $b*$k }]
                set g $p
                set b $b
            }
            5   {
                set k [expr { 1.0-($f*$s) }]
                set r $b
                set g $p
                set b [expr { $b*$k }]
            }
        }

        # Adjust the rgb channels values if they exceeds their limits [0,1.0].
        if { $r < 0 } {
            set r 0
        } elseif { $r > 1.0 } {
            set r 1.0
        }

        if { $g < 0 } {
            set g 0
        } elseif { $g > 1.0 } {
            set g 1.0
        }

        if { $b < 0 } {
            set b 0
        } elseif { $b > 1.0 } {
            set b 1.0
        }

        set perceived_brightness [expr { (sqrt(($r*$r*$::sRGB(unadapted,Yr))+($g*$g*$::sRGB(unadapted,Yg))+($b*$b*$::sRGB(unadapted,Yb))))*100.0 }]

        # Adjust the perceived brightness value if exceeds its limits [0,100.0].
        if { $perceived_brightness < 0 } {
            set perceived_brightness 0
        } elseif { $perceived_brightness > 100.0 } {
            set perceived_brightness 100.0
        }

        set max [expr { max($r,$g,$b) }]

        if { $r == $g && $r == $b } {
            # It's a grey...
            set hue        0
            set saturation 0
        } elseif { $r == $max } {
            if { $b < $g } {
                set saturation [expr { (1.0-($b/$r))*100.0 }]
            } else {
                # b >= g
                set saturation [expr { (1.0-($g/$r))*100.0 }]
            }
        } elseif { $g == $max } {
            if { $r < $b } {
                set saturation [expr { (1.0-($r/$g))*100.0 }]
            } else {
                # r >= b
                set saturation [expr { (1.0-($b/$g))*100.0 }]
            }
        } else {
            # b = max
            if { $g < $r } {
                set saturation [expr { (1.0-($g/$b))*100.0 }]
            } else {
                # g >= r
                set saturation [expr { (1.0-($r/$b))*100.0 }]
            }
        }

        # Adjust the saturation value if exceeds its limits [0,100.0].
        if { $saturation < 0 } {
            set saturation 0
        } elseif { $saturation > 100.0 } {
            set saturation 100.0
        }

        lappend results $hue $saturation $perceived_brightness
    }

    return $results
}

#*EOF*
